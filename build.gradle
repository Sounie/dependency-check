plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.39.0'
}


group 'sounie'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

test {
    jvmArgs = ['-Xmx1500M']

    useJUnitPlatform()
}

dependencies {
    compile(
            'io.github.classgraph:classgraph:4.8.110',
            'org.slf4j:slf4j-api:1.7.32'
    )


    testImplementation(
            'com.sun.jersey:jersey-client:1.19.4',
            'org.apache.hadoop:hadoop-client:3.3.1'
    )

    testImplementation('org.junit.jupiter:junit-jupiter:5.7.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.2')
}

publishing {
    publications {
        sounieDependencyCheck(MavenPublication) {
            from components.java

            pom {
                description = 'This is a generated file to enable github to report dependencies.  Please use ./gradlew for building.'
            }
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    checkForGradleUpdate = true

    outputFormatter = 'json,plain'
    outputDir = "${project.buildDir}/reports/dependencies"
    reportfileName = 'versions'

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}